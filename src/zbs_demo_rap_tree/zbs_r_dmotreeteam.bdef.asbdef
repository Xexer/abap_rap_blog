managed implementation in class ZBP_BS_DEMO_TREE_TEAM unique;
strict ( 2 );
with draft;

define behavior for ZBS_R_DMOTreeTeam alias Team
persistent table zbs_dmo_team
draft table zbs_dmo_team_d
etag master LocalLastChanged
lock master total etag LastChanged
authorization master ( global )
with additional save
{
  field ( mandatory : create )
  UserId;

  field ( readonly )
  LocalCreatedBy,
  LocalLastChangedBy,
  LocalLastChanged,
  LastChanged;

  field ( readonly : update )
  UserId;

  create;
  update;
  delete;

  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;
  draft determine action Prepare;

  association _TeamLeader { with draft; }
  association _Member { with draft; }

  instance hierarchy ZBS_I_DMOTreeTeamHR
  {
//    managed reorder action changeNextSibling;
//    field ( hierarchy-index ) SiblingOrderNumber;
    ascending association _TeamLeader;
    descending association _Member { with cascading delete; }
  }

  mapping for zbs_dmo_team
    {
      UserId             = user_id;
      PlayerName         = player_name;
      PlayerEmail        = player_email;
      PlayerPosition     = player_position;
      Score              = score;
      Team               = team;
      TeamLeader         = team_leader;
      LocalCreatedBy     = local_created_by;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChanged   = local_last_changed;
      LastChanged        = last_changed;
    }
}