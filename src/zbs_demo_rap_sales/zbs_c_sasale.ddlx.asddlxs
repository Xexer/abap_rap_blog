@Metadata.layer: #CORE
@UI.headerInfo.title.type: #STANDARD
@UI.headerInfo.title.value: 'PartnerNumber'
@UI.headerInfo.description.type: #STANDARD
@UI.headerInfo.description.value: 'SalesDate'
annotate view ZBS_C_SASALE with
{
  @UI.facet: [
    {
      id: 'idGeneral',
      label: 'General Information',
      position: 10 ,
      type: #IDENTIFICATION_REFERENCE,
      targetQualifier: 'GENERAL'
    },
    {
      id: 'idDifference',
      label: 'Difference',
      position: 20,
      type: #IDENTIFICATION_REFERENCE,
      targetQualifier: 'DIFF'
    },
    {
      id: 'idInformation',
      label: 'Information',
      position: 30,
      type: #IDENTIFICATION_REFERENCE,
      targetQualifier: 'INFO'
    },
    {
      id: 'idAdmin',
      label: 'Admin Fields',
      position: 40,
      type: #IDENTIFICATION_REFERENCE,
      targetQualifier: 'ADMIN'
    },
    {
      id: 'idSASeller',
      label: 'Seller',
      position: 50,
      type: #LINEITEM_REFERENCE,
      targetElement: '_SASeller'
    },
    {
      id: 'idSASold',
      label: 'Sold',
      position: 60,
      type: #LINEITEM_REFERENCE,
      targetElement: '_SASold'
    },
    {
      id: 'idSAInfo',
      label: 'Info',
      position: 70,
      type: #LINEITEM_REFERENCE,
      targetElement: '_SAInfo'
    }
  ]

  @UI.lineItem: [
    { position: 10, type: #FOR_ACTION, dataAction: 'createFixValue', label: 'Create Fix Value' },
    { position: 20, type: #FOR_ACTION, dataAction: 'createPercent', label: 'Create Percent' }
  ]
  @UI.identification: [
    { position: 10, type: #FOR_ACTION, dataAction: 'ConsistencyCheck', label: 'Check consistency', criticality: 'ButtonCriticality' },
    { position: 20, type: #FOR_ACTION, dataAction: 'ClearDifferences', label: 'Clear', qualifier: 'DIFF', inline: true }
  ]
  @UI.hidden: true
  UUID;

  @UI.selectionField: [ { position: 10 } ]
  @UI.lineItem: [ { position: 20  } ]
  @UI.identification: [ { position: 20 , qualifier: 'GENERAL' } ]
  @EndUserText.label: 'Partner'
  PartnerNumber;

  @UI.selectionField: [ { position: 20 } ]
  @UI.lineItem: [ { position: 30  } ]
  @UI.identification: [ { position: 30 , qualifier: 'GENERAL' } ]
  @EndUserText.label: 'Sales Date'
  SalesDate;

  @UI.lineItem: [ { position: 32  } ]
  @UI.identification: [ { position: 32 , qualifier: 'GENERAL' } ]
  @EndUserText.label: 'Sales Year'
  SalesYear;

  @UI.lineItem: [ { position: 33  } ]
  @UI.identification: [ { position: 33 , qualifier: 'GENERAL' } ]
  @EndUserText.label: 'Sales Month'
  SalesMonth;

  @UI.lineItem: [ { position: 40  } ]
  @UI.identification: [ { position: 40 , qualifier: 'GENERAL' } ]
  @EndUserText.label: 'Sales Volume'
  SalesVolume;

  @UI.selectionField: [ { position: 30 } ]
  @UI.lineItem: [ { position: 50  } ]
  @UI.identification: [ { position: 50 , qualifier: 'GENERAL' } ]
  @EndUserText.label: 'Sales Currency'
  SalesCurrency;

  @UI.lineItem: [ { position: 55, type: #AS_FIELDGROUP, valueQualifier: 'DIFF_NUM', label: 'Difference' } ]
  @UI.fieldGroup: [{ qualifier: 'DIFF_NUM', hidden: #(isAmountHidden) }]
  @UI.identification: [ { position: 60 , qualifier: 'DIFF' } ]
  @EndUserText.label: 'Difference Amount'
  DifferenceAmount;

  @UI.lineItem: [ { position: 57, type: #AS_FIELDGROUP, valueQualifier: 'DIFF_UNIT', label: 'Difference (Unit)' } ]
  @UI.fieldGroup: [{ qualifier: 'DIFF_UNIT', hidden: #(isAmountHidden) }]
  @UI.identification: [ { position: 70 , qualifier: 'DIFF' } ]
  @EndUserText.label: 'Currency'
  DifferenceCurrency;

  @UI.fieldGroup: [{ qualifier: 'DIFF_NUM', hidden: #(isQuantityHidden) }]
  @UI.identification: [ { position: 80 , qualifier: 'DIFF' } ]
  @EndUserText.label: 'Difference Quantity'
  DifferenceQuantity;

  @UI.fieldGroup: [{ qualifier: 'DIFF_UNIT', hidden: #(isQuantityHidden) }]
  @UI.identification: [ { position: 90 , qualifier: 'DIFF' } ]
  @EndUserText.label: 'Unit'
  DifferenceUnit;

  @UI.identification: [ { position: 100 , qualifier: 'INFO' } ]
  @EndUserText.label: 'Comment'
  SaleComment;

  @UI.identification: [ { position: 105, qualifier: 'GENERAL' } ]
  @EndUserText.label: 'Log ID'
  LoggingId;

  @UI.identification: [ { position: 110 , qualifier: 'ADMIN' } ]
  @EndUserText.label: 'Created By'
  LocalCreatedBy;

  @UI.identification: [ { position: 120 , qualifier: 'ADMIN' } ]
  @EndUserText.label: 'Created At'
  LocalCreatedAt;

  @UI.identification: [ { position: 130 , qualifier: 'ADMIN' } ]
  @EndUserText.label: 'Local Changed By'
  LocalLastChangedBy;

  @UI.identification: [ { position: 140 , qualifier: 'ADMIN' } ]
  @EndUserText.label: 'Local Changed At'
  LocalLastChangedAt;

  @UI.identification: [ { position: 150 , qualifier: 'ADMIN' } ]
  @EndUserText.label: 'Last Changed At'
  LastChangedAt;

  @UI.identification: [ { position: 160 , qualifier: 'GENERAL' } ]
  @EndUserText.label: 'Text Information'
  TextInformation;

  //  @UI.hidden: true
  //  isAmountHidden;
  //
  //  @UI.hidden: true
  //  isQuantityHidden;

  @UI.identification: [{ position: 160 , qualifier: 'GENERAL', value: '_SASold.MaterialId' }]
  @UI.lineItem: [ { position: 160, value: '_SASold.MaterialId'  } ]
  _SASold;
}