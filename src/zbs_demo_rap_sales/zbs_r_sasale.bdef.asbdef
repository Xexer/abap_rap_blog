managed implementation in class ZBS_BP_R_SASALE unique;
strict ( 2 );
with draft;
extensible;
define behavior for ZBS_R_SASALE alias SASale
persistent table zbs_sasale
extensible
draft table zbs_sasale_d
etag master LocalLastChangedAt
lock master total etag LastChangedAt
authorization master ( global )
{
  field ( readonly )
  UUID,
  LocalCreatedBy,
  LocalCreatedAt,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;

  field ( numbering : managed )
  UUID;

  static factory action ( authorization : global ) createFixValue parameter ZBS_S_SAPopupFixValue [1];
  static factory action ( authorization : global ) createPercent parameter ZBS_S_SAPopupPercent [1];
  action ConsistencyCheck;
  action ClearDifferences;

  create;
  update;
  delete;

  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;
  draft determine action Prepare;

  mapping for zbs_sasale corresponding extensible
    {
      UUID               = uuid;
      PartnerNumber      = partnernumber;
      SalesDate          = salesdate;
      SalesVolume        = salesvolume;
      SalesCurrency      = salescurrency;
      DifferenceAmount   = differenceamount;
      DifferenceCurrency = differencecurrency;
      DifferenceQuantity = differencequantity;
      DifferenceUnit     = differenceunit;
      SaleComment        = salecomment;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
      LoggingId          = logging_id;
    }

  association _SAInfo { create; with draft; }
  association _SASold { create; with draft; }
  association _SASeller { create; with draft; }

}

define behavior for ZBS_R_SAINFO alias SAInfo
persistent table zbs_sainfo
extensible
draft table zbs_sainfo_d
etag dependent by _SASale
lock dependent by _SASale
authorization dependent by _SASale
{
  field ( readonly )
  ParentUUID;

  field ( readonly : update ) Language;

  update;
  delete;

  mapping for zbs_sainfo corresponding extensible
    {
      ParentUUID      = parent_uuid;
      Language        = language;
      TextInformation = text000;
    }

  association _SASale { with draft; }

}

define behavior for ZBS_R_SASOLD alias SASold
persistent table zbs_sasold
extensible
draft table zbs_sasold_d
etag dependent by _SASale
lock dependent by _SASale
authorization dependent by _SASale
{
  field ( readonly )
  UUID,
  ParentUUID;

  field ( numbering : managed )
  UUID;

  action ReleaseItems;

  update;
  delete;

  mapping for zbs_sasold corresponding extensible
    {
      UUID       = uuid;
      ParentUUID = parent_uuid;
      MaterialId = materialid;
    }

  association _SASale { with draft; }

}

define behavior for ZBS_R_SASELLER alias SASeller
persistent table zbs_saseller
extensible
draft table zbs_saseller_d
etag dependent by _SASale
lock dependent by _SASale
authorization dependent by _SASale
{
  field ( readonly )
  UUID,
  ParentUUID;

  field ( numbering : managed )
  UUID;


  update;
  delete;

  mapping for zbs_saseller corresponding extensible
    {
      UUID       = uuid;
      ParentUUID = parent_uuid;
      SellerId   = sellerid;
      Quota      = quota;
      Confirmed  = confirmed;
    }

  association _SASale { with draft; }

}